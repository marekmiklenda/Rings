# Simple sorting program
#
# Takes a list of integers (max 15) on stdin terminated by EOF (0xFF) and outputs them to stdout, terminated by EOF.
# This task could be solved using only two rings.
# Also no error handling or exit codes.

mkr 15                      # Ring 0 – Input
mkr 1 put 1 1               # Ring 1 – Constant 1
mkr 1 put 2 0xF             # Ring 2 – Constant 0xF
mkr 1 put 3 0xFF            # Ring 3 – Constant 0xFF
mkr 1                       # Ring 4 – Data length counter
mkr 1                       # Ring 5 – Counter 2
mkr 1                       # Ring 6 – Counter 3
mkr 1                       # Ring 7 – Output cell

:load_inputs                # Loading input data until EOF
    rot 0 1
    inp 0
    jeq 0 3 :loading_end
    add 4 1 4
    jmp :load_inputs
:loading_end

jeq 4 5 :end                # If no data, skip to end.
sub 2 4 5                   # Remaining free space to fill

:garbage_fill               # Filling the rest of the array with garbage 0xFF
    put 0 0xFF
    rot 0 1
    sub 5 1 5
    jgt 5 6 :garbage_fill

:main_loop                  # Iterate, always find the smallest value and output it
    put 7 0xFF
    :nested_loop
        rot 0 1
        jgt 0 7 :no_swap
            swp 0 7
        :no_swap
        add 5 1 5
        jlt 5 2 :nested_loop
    out 7
    put 5 0
    add 6 1 6
    jlt 6 4 :main_loop

:end
out 3                       # Done. Emit EOF and end